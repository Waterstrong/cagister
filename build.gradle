buildscript {
    repositories {
        mavenCentral()
    }
    dependencies {
        classpath("org.springframework.boot:spring-boot-gradle-plugin:1.3.3.RELEASE")
        classpath("org.flywaydb:flyway-gradle-plugin:4.0")
    }
}

apply plugin: 'java'
apply plugin: 'idea'
apply plugin: 'spring-boot'
apply plugin: 'org.flywaydb.flyway'
apply plugin: 'application'
apply plugin: 'checkstyle'
apply from: 'jacoco.gradle'

repositories {
    mavenCentral()
}

dependencies {
    compile("org.springframework.boot:spring-boot-starter-web")
    compile("org.springframework.boot:spring-boot-starter-hateoas")
    compile("org.springframework.boot:spring-boot-starter-data-jpa")
    compile("com.h2database:h2")
    compile("org.apache.logging.log4j:log4j:2.5")
    compile("org.flywaydb:flyway-core:4.0")
    compile("mysql:mysql-connector-java:6.0.2")
    compile("io.springfox:springfox-swagger2:2.4.0")
    compile("io.springfox:springfox-swagger-ui:2.4.0")
    compile("ma.glasnost.orika:orika-core:1.4.6")
    testCompile("com.jayway.jsonpath:json-path")
    testCompile("org.springframework.boot:spring-boot-starter-test")
}

mainClassName = "tw.dojo.pos.Application"

jar {
    baseName = "cagister"
    version = "0.1.0"
}

sourceCompatibility = "1.8"
targetCompatibility = "1.8"
idea.project.vcs = "Git"

flyway {
    url = 'jdbc:h2:./.tmp/cagisterdb'
    user = 'sa'
}

sourceSets {
    main {
        java.srcDirs = ['src/main/java']
        resources.srcDirs = ['src/main/resources']
    }
    test {
        java.srcDirs = ['src/test/unit/java', 'src/test/intg/java']
        resources.srcDirs = ['src/test/unit/resources', 'src/test/intg/resources']
    }
}

defaultTasks 'clean', 'build'

clean.dependsOn flywayRepair
build.dependsOn flywayMigrate

test {
    exclude '**/*IntegrationTest.class'
}
task unitTest(dependsOn: test)

task integrationTest(type: Test) {
    include '**/*IntegrationTest.class'
}
build.dependsOn integrationTest

task wrapper(type: Wrapper) {
    gradleVersion = "2.13"
}